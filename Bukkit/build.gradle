repositories {
    maven { url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url = "https://jitpack.io" }
    maven { url = "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url = "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
    mavenLocal()
}

dependencies {
    implementation(project(":Core"))
    implementation("org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT")
    implementation("com.github.MilkBowl:VaultAPI:1.7.1") {
        exclude module: 'bukkit'
    }
    implementation("me.clip:placeholderapi:2.11.5")
}

sourceCompatibility = 17
targetCompatibility = 17

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from("src/main/resources") {
        include "plugin.yml"
        expand(
                name: project.parent.name,
                version: project.parent.version
        )
    }
}

//noinspection GroovyAssignabilityCheck
jar.archiveFileName = "plotsquared-bukkitapi-${project.parent.version}.jar"
jar.destinationDirectory = file("../mvn/com/plotsquared/plotsquared-bukkitapi/" + project.parent.version)

publishing {
    publications {
        createPom(MavenPublication) {
            groupId = rootProject.group
            artifactId = "plotsquared-bukkitapi"
            version = rootProject.version

            pom.withXml {
                asNode().appendNode('distributionManagement').appendNode('repository').appendNode('id', 'internal.repo')
                asNode().appendNode('distributionManagement').appendNode('repository').appendNode('url', "../mvn/com/plotsquared/plotsquared-bukkitapi/${project.parent.version}/plotsquared-bukkitapi-${project.parent.version}.pom")
            }
        }
        createPomLatest(MavenPublication) {
            groupId = "com.plotsquared"
            artifactId = "plotsquared-bukkitapi"
            version = "latest"

            pom.withXml {
                asNode().appendNode('distributionManagement').appendNode('repository').appendNode('id', 'internal.repo')
                asNode().appendNode('distributionManagement').appendNode('repository').appendNode('url', "../mvn/com/plotsquared/plotsquared-bukkitapi/latest/plotsquared-bukkitapi-latest.pom")
            }
        }
    }
}

task copyFiles {
    doLast {
        copy {
            from("../mvn/com/plotsquared/plotsquared-bukkitapi/${project.parent.version}/")
            into("../mvn/com/plotsquared/plotsquared-bukkitapi/latest/")
            include("plotsquared-bukkitapi*.jar")
            rename("plotsquared-bukkitapi-${project.parent.version}.jar", "plotsquared-bukkitapi-latest.jar")
        }
    }
}

shadowJar {
    dependencies {
        include(dependency(":Core"))
    }
    archiveFileName = "${parent.name}-${project.name}-${parent.version}.jar"
    getDestinationDirectory().set(file("../target"))
}

shadowJar.doLast {
    task ->
        ant.checksum file: task.archivePath
}

build.dependsOn(shadowJar)
build.finalizedBy(copyFiles)
copyFiles.dependsOn('publish')